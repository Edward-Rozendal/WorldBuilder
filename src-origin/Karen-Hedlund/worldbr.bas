10 CLS
20 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT TAB(12) "WORLD BUILDER"
30 PRINT TAB(11) "BY STEVEN KIMMEL"
40 PRINT:PRINT TAB(12) "A PROGRAM FOR DESIGNING WORLDS"
50 PRINT TAB(11) "WHERE NO MAN HAS BEEN BEFORE"
60 FOR I=1 TO 15000: NEXT
70 DIM SC$(8),M(8),C$(8),S$(36),LS(36),SS$(36),SM(36),MP(15),R(15)
80 FOR I=1 TO 8:READ SC$(I),M(I),C$(I):NEXT I
90 FOR I=1 TO 17:READ S$(I),SS$(I),SM(I):LS(I)=SM(I)^3.5:NEXT
100 FOR I=18 TO 36:READ S$(I),SS$(I),LS(I):SM(I)=LS(I)^.285714:NEXT
110 DATA 0,100,BLUE,B,17,PALE BLUE,A,3.2,WHITE,F,1.54,PALE YELLOW
120 DATA G,1.02,YELLOW,K,.75,ORANGE,M,.38,RED,D,0.,RED
130 DATA SOL,G2,1.0,ALPHA CENTAURI A,G4,1.08,ALPHA CENTAURI B,K1,.88
140 DATA EPSILON ERIDANI,K2,.80,TAU CETI,G8,.82
150 DATA 70 OPHIUCHI A,K1,.9,70 OPHIUCHI B,K5,.65
160 DATA ETA CASSIOPEIAE A,F9,.94,ETA CASSIOPEIAE B,K6,.58
170 DATA SIGMA DRACONIS,G9,.82,36 OPHIUCHI A,K2,.77
180 DATA 36 OPHIUCHI B,K1,.76,HR 7703,K2,.75
190 DATA DELTA PAVONIS,GY,.98,82 ERIDANI,G5,.91
200 DATA BETA HYDRI,G1,1.23,HR 8832,K3,.74
210 DATA SIRIUS,A1,23,CANOPUS,F0,130,VEGA,A0,52,ARCTURUS,K2,100
220 DATA RIGEL, B8,52000,CAPELLA,G8,145,PROCYON,F5,7.6
230 DATA ACHERNAR,B5,1000,ALTAIR,A7,10,BETELGUESE,M2,8300
240 DATA ALDEBARAN,K5,160,SPICA,B1,760,BARNARD'S STAR,M5,.00044
250 DATA POLLUX,K0,33,FOMALHAUT,A3,13,BETA CRUCIS,B0,8300
260 DATA DENEB,A2,52000,REGULUS,B7,160,BARNARD'S STAR,M5,.00044
270 CLS:PRINT "WORLD BUILDER"
280 PRINT "Enter the number for the option you want"
290 PRINT:PRINT "1....Use a known star"
300 PRINT "2....Use a star not on list"
310 PRINT "3....List known stars"
320 PRINT "4....Quit"
330 PRINT
340 INPUT "YOUR CHOICE";A
350 IF A<1 OR A>4 GOTO 270
360 ON A GOTO 430  ,570  ,390  ,370
370 INPUT "Are you sure you want to quit";A$
380 IF ASC(A$)=89 THEN END ELSE GOTO 270
390 CLS:PRINT "I KNOW THE FOLLOWING STARS"
400 FOR I=1 TO 12
410 PRINT S$(I);TAB(20);S$(I+12);TAB(40);S$(I+24)
420 NEXT I
430 INPUT "WHICH STAR SHOULD I USE";S$
440 IF S$="NONE" OR S$="" THEN 270
450 FOR I=1 TO 36
460 IF S$=S$(I) THEN SK=I:GOTO 490
470 NEXT I
480 PRINT "I DON'T KNOW THAT STAR" :GOTO 530
490 SC=VAL(RIGHT$(SS$(SK),1))/10
500 S1$=LEFT$(SS$(SK),1)
510 FOR I=1 TO 7:IF S1$=SC$(I) THEN J=I:GOTO 530
520 NEXT I
530 MS=SM(SK):L=LS(SK):AS=(MS^-2.5)*10
540 P=(1.25-L^.285714)/MS/4.999762E-03
550 IF P/100*AS>10 THEN P=1000/AS
560 GOTO 760
570 INPUT "WHAT IS THE STAR'S NAME";S$
580 INPUT "WHAT IS THE STAR'S SPECTRAL CLASS";S1$
590 IF S1$<>"" THEN GOTO 660
600 INPUT "WHAT IS THE ABSOLUTE MAGNITUTDE (SUN=4.85)";M
610 L=EXP(4.4228-.9195*M): MS=L^.285714
620 FOR I=1 TO 7:IF M(I)<MS THEN J=I-1:GOTO 640
630 NEXT I
640 S1$=SC$(I-1):SC=INT((MS-M(J))/(M(I)-M(J)))/10
650 GOTO 700
660 SC=VAL(RIGHT$(S1$,1))/10 : S1$=LEFT$(S1$,1)
670 FOR I=1 TO 7:IF S1$=SC$(I) THEN J=I:GOTO 690
680 NEXT I:PRINT "I DON'T KNOW THAT CLASS":GOTO 580
690 MS=M(J)-SC*(M(J)-M(J+1))
700 AS=(MS^-2.5)*10
710 PRINT S$;" HAS AN EXPECTED LIFE OF ";AS;" BILLION YEARS"
720 INPUT "WHAT PERCENT (1-100) HAS ALREADY PASSED";P
730 IF P/100*AS>12 THEN PRINT "THE BIG BANG OCCURED 12 BILLION YEARS AGO.";INPUT "IS THIS WHAT YOU WANT";A$:IF ASC(A$)<>89 THEN 710
740 MS=MS*(1.25-4.999762E-03*P)
750 L=MS^3.5
760 TS=6000*MS^.35
770 DS=MS^.3333
780 CLS:PRINT "STELLAR DATA"
790 PRINT "THE SELECTED STAR, ";S$;", IS A ";S1$;SC*10;" STAR"
800 IF SC>.75 THEN PRINT"IS ";C$(J+1) : GOTO 830
810 IF SC<.25 THEN PRINT"IS ";C$(J); : GOTO 830
820 PRINT "IS BETWEEN ";C$(J);" AND ";C$(J+1)
830 PRINT "HAS A MASS OF ";MS;" TIMES THAT OF THE SUN"
840 PRINT "IT IS ";L;" TIMES AS BRIGHT AS THE SUN"
850 PRINT "THE STAR HAS AN EXPECTED LIFESPAN OF";AS;" BILLION YEARS"
860 PRINT "OF WHICH IT HAS LIVED";P;"% OR ABOUT"AS*P/100;"BILLION YEARS."
870 IF P>95 THEN PRINT"  THE STAR IS IN ITS DEATH THROES."
880 PRINT "IT HAS A SURFACE TEMPATURE OF ";TS;" DEGREES KELVIN"
890 IF J+SC<2.5 OR J+SC>7 THEN PRINT "AND IS BELIEVED TO HAVE NO PLANETS":GOTO 910 
900 PRINT "AND MAY HAVE PLANETS"
910 PRINT "THIS STAR WILL DIE AS A ";
920 IF MS<1.5 THEN PRINT "WHITE DWARF":GOTO 960
930 IF MS<4! THEN PRINT "NEUTRON STAR": GOTO 960
940 IF MS<10! THEN PRINT "NEUTRON STAR": GOTO 960
950 PRINT "BLACK HOLE AFTER GOING SUPER NOVA"
960 INPUT "ANOTHER STAR";A$
970 IF ASC(A$)=89 THEN 270
980 P=P/100
990 CLS:PRINT "THE MAIN PLANET OF INTEREST"
1000 PRINT "THE EARTH HAS AN AVERAGE SURFACE TEMPERATURE OF 60 DEGREES"
1010 INPUT "WHAT SURFACE TEMPERATURE WOULD YOU LIKE";TP:TP=TP+460
1020 INPUT "DESIRED SURFACE GRAVITY (EARTH=1)";G
1030 IF G<=0 THEN PRINT "THE PLANET MUST HAVE SOME GRAVITY":GOTO 1020
1040 RP=SQR(L/(TP/520)^4)
1050 IF RP<MS/5 THEN PRINT "THIS PLANET IS TOO CLOSE TO BE STABLE.":GOTO 1000
1060 PP=SQR(RP^3/MS)
1070 IS=L/RP^2 
1080 RM=.00012*TS
1090 RX=6.452001E-02*EXP(.0005*TS)
1100 SA=DS/RP
1110 INPUT "HOW BIG SHOULD THE PLANET BE RELATIVE TO EARTH";D
1120 M=G*D^2
1130 IF M<.055 THEN PRINT "THE PLANET WON'T RETAIN AN OXYGEN ATMOSPHERE"
1140 IF M>17.6 THEN PRINT "THE PLANET WON'T LOSE ITS HYDROGEN ATMOSPHERE"
1150 PRINT "EARTH'S ORBIT HAS AN ECCENTRCITY OF .01672"
1160 INPUT "WHAT IS THE ORBITAL ECCENTRICITY ( <1)";EC
1170 IF EC>1 THEN 1160
1180 CA=(1-EC)*RP : FA=(1+EC)*RP
1190 INPUT "HOW DOES THE AXIS TILT (EARTH=23.5 DEGREES)";TI
1200 IF TI<0 OR TI>90 THEN 1190
1210 INPUT "HOW MANY MOONS DOES THE PLANET HAVE";MN
1220 IF MN>10 THEN PRINT "FOR CONVENIENCE WE'LL LIMIT THIS TO 10":MN=10
1230 MM=1000:H=0:R=56*G
1240 IF MN<=0 THEN 1340
1250 FOR I=1 TO MN
1260 PRINT "MASS OF MOON #";I;" (OUR MOON=1)";:INPUT MN(I)
1270 INPUT "ORBIT (OUR MOON=30)";MR(I)
1280 IF MR(I)<3*G THEN PRINT "THE MOON IS TOO CLOSE AND WILL BREAK UP":GOTO 1270
1290 IF MR(I)>56*G THEN PRINT"THE MOON IS TOO FAR AND WILL DRIFT AWAY":GOTO 1270
1300 MP(I)=SQR(MR(I)^3/M)*4
1310 IF MR(I)<R THEN MM=MP(I):R=MR(I)
1320 H=MN(I)*.01235/(MR(I)^3)+H
1330 NEXT I
1340 H2=.8499847*D^4/M*(MS*333484!/(11750*RP)^3+H)
1350 DA=1759260!*H2*14+10
1360 IF DA>MM THEN DA=MM
1370 CLS:PRINT "THIS PLANET'S DAY SHOULD BE ABOUT";DA;" HOURS LONG."
1380 PRINT "THAT MAKES ITS YEAR";8766/DA*PP;"DAYS LONG.  WITH A TILT OF";TI
1390 HI=(1+.025*DA/24)*TP-460 : LO=(1-.025*DA/24)*TP-460
1400 IF LO<-460 THEN LO=-460
1410 PRINT "TODAY'S HIGH TEMPERATURE SHOULD BE";HI;" DEGREES F."
1420 PRINT "TONIGHT'S EXPECTED LOW IS";LO;" DEGREES F."
1430 SH=HI+1.9*TI*(1+EC) ^2 : LL=LO-1.9*TI/(1+EC)^2
1440 IF LL<-460 THEN =-480
1450 PRINT "THIS SUMMER WE EXPECT IT TO GET UP TO";SH
1460 PRINT "THIS WINTER IT SHOULD DROP DOWN TO";LL
1470 IF SH<32 OR LL>175 THEN PRINT "THERE ARE TIMES WHEN NO LIQUID WATER EXISTS."
1480 IF MN<=0 GOTO 1620
1490 IF MN=1 GOTO 1590
1500 FOR I=1 TO MN:F=0:FOR K=1 TO MN-I
1510 IF MR(K+1)>=MR(K) THEN 1560
1520 T=MR(K):MR(K)=MR(K+1):MR(K+1)=T
1530 T=MN(K):MN(K)=MN(K+1):MN(K+1)=T
1540 T=MP(K):MP(K)=MP(K+1):MP(K+1)=T
1550 F=1
1560 NEXT K
1570 IF F=0 THEN 1590
1580 NEXT I
1590 PRINT:PRINT "YOUR SELECTED SYSTEM ON MOONS"
1600 PRINT "ORBIT","MASS","PERIOD"
1610 FOR I=1 TO MN:PRINT MR(I),MN(I),MP(I)/DA;"DAYS":NEXT I
1620 INPUT "WANT A DIFFERENT SET OF MOONS";A$
1630 IF ASC(A$)=89 THEN 1210
1640 CLS:PRINT "PLANETARY DATA"
1650 PRINT "OUR PRINCIPAL PLANET OF INTEREST HAS AN AVERAGE SURFACE "
1660 PRINT "TEMPERATURE OF";TP-460;" DEGREES F. THIS REQUIRES AN ORBIT"
1670 PRINT "OF";RP;" ASTRONOMICAL UNITS (";RP*93;" MILLION MILES)"
1680 PRINT "CLOSEST APPROACH =";CA;" AU: GREATEST DISTANCE =";FA;" AU"
1690 PRINT "THIS ALSO MEANS IT HAS A YEAR THAT IS";PP;" YEARS LONG"
1700 PRINT "THE STAR APPEARS";
1710 IF SA>1.5 OR SA<.75 THEN PRINT " MUCH";
1720 IF SA>1! THEN PRINT" LARGER";: ELSE PRINT " SMALLER";
1730 PRINT " THAN OUR SUN."
1740 IF G>.95 AND G<1.05 THEN PRINT "GRAVITY IS ESSENTIALLY THE SAME AS EARTH'S":GOTO 1830
1750 PRINT "SINCE OUT PLANET HAS A GRAVITY";:IF G<1 THEN 1810
1760 PRINT "GREATER THAN EARTH'S WE EXPECT"
1770 PRINT "A THICKER ATMOSPHERE.  THERE IS GREATER TECTONIC ACTION AND MUCH"
1780 PRINT "GREATER RESISTING FORCES.  THUS WE EXPECT MORE CONTINENTS AND"
1790 PRINT "SHORTER MOUNTAINS.  EARTHQUAKES SHOULD BE MORE FREQUENT AND MORE"
1800 PRINT "SEVERE.":GOTO 1830
1810 PRINT " LESS THAN EARTH'S WE EXPECT"
1820 PRINT "A THINNER ATMOSPHERE.  THERE IS LESS TECTONIC ACTION AND LESS"
1830 PRINT "A GRAVITY OF ";G;" MEANS THAT IF YOU WEIGH 200 POUNDS"
1840 PRINT "TALLER.  EARTHQUAKES, IF ANY, WILL BE LESS SEVERE."
1850 PRINT "YOU WOULD WEIGH";G*200;" ON OUR PLANET"
1860 INPUT "WOULD YOU LIKE A NEW GRAVITY";A$
1870 IF ASC(A$)=89 THEN 1020
1880 CLS:PRINT "LIFE ???"
1890 IF M<.055 OR M>17.6 THEN PRINT "BECAUSE OF THE BAD ATMOSPHERE ":GOTO 2240
1900 IF RP<RM OR RP>RX THEN PRINT "BECAUSE OF THE LEVEL OF RADIATION ":GOTO 2240
1910 IF SH<32 OR LL>175 THEN PRINT "SINCE LIQUID WATER NEVER OCCURS ":GOTO 2240
1920 IF AS*P <1.5 THEN PRINT "THE PLANET IS TOO YOUNG TO HAVE EVOLVED LIFE":GOTO 2240
1930 IF P>.95 THEN PRINT "SINCE THE STAR IS IN ITS DEATH THROES ":GOTO 2240
1940 PRINT "THERE MAY BE SOME ";
1950 IF AS*P<2*G THEN PRINT "BACTERIA AND BLUE GREEN ALGAE":GOTO 2220
1960 IF AS*P<3*G THEN PRINT "SINGLE CELL LIFE WITH NUCLEUS":GOTO 2220
1970 IF AS*P<4*G THEN PRINT "SIMMPLE MULTICELLED LIFE":GOTO 2220
1980 IF AS*P<4.4*G THEN PRINT "WATER VERTEBRATES AND LAND PLANTS":GOTO 2220
1990 PRINT "MAJOR LAND ANIMALS AND PERHAPS INTELLEGENCE"
2000 IF G<.95 THEN 2090
2010 IF G<1.05 THEN 2170
2020 PRINT "HIGHER GRAVITY MEANS A THICK ATMOSPHERE WHICH WILL SUPPORT LARGE"
2030 PRINT "BIRDS.  IT ALSO MEANS THAT SHORT FALLS COULD BE FATAL SO REACTION"
2040 PRINT "TIMES SHOULD BE VERY SHORT.  ALL LIFE FORMS WILL BE SORTER AND"
2050 PRINT "STOCKIER THAN ON EARTH."
2060 IF G>1.199969 THEN PRINT "THERE ARE NO TWO LEGGED ANIMALS."
2070 PRINT "THE THICK ATMOSPHERE IMPROVES SOUND TRANSMISSION SO THE ANIMALS MAY"
2080 PRINT "RELY MORE ON HEARING.":GOTO 2170
2090 PRINT "LOWER GRAVITY MEANS THINNER ATMOSPHERE.  BIRDS, IF ANY, WILL HAVE"
2100 PRINT "LARGER WINGS.  ALL LIFE FORMS SHOULD BE TALLER AND MORE SLENDER"
2110 PRINT "THAN ON EARTH."
2120 PRINT "THERE PROBABLY ARE MANY TWO LEGGED ANIMALS."
2130 PRINT "THE THIN ATMOSPHERE HURTS SOUND TRANSMISSION SO ANIMALS WILL"
2140 PRINT "EITHER HAVE LARGE EARS OR NONE.  LUNGS WILL BE MUCH LARGER."
2150 IF TP>75 THEN PRINT "SOME FORM OF RADIATION PROTECTION WILL BE NECESSARY."
2160 IF SA<.75 THEN PRINT "LARGE EYES OR RELY ON OTHER SENSES."
2170 IF SA<.75 THEN PRINT "BECAUSE OF THE SMALL SUN, WE EXPECT ANIMALS TO HAVE"
2180 IF SA>1.5 THEN PRINT "UNLESS THE ATMOSPHERE IS OBSCURED, WE EXPECT RELIANCE"
2190 IF SA>1.5 THEN PRINT "ON SIGHT USING RELATIVELY SMALL EYES."
2200 IF HI-LO>50 THEN PRINT "EXTREME TEMPERATURE VARIATIONS FAVOR UNDERGROUND AND"
2210 IF HI-LO>50 THEN PRINT "UNDERWATER LIFEFORMS."
2220 IF (TP-460)<32 OR (TP-460)>86 OR G>1.5 OR G<.68 OR M<.4 OR M>2.35 OR DA>96 OR SH>120 OR LL<-60 OR HI>120 OR LO<-10 THEN HM=0 ELSE HM=1
2230 GOTO 2250
2240 PRINT "THERE APPEARS TO BE NO LIFE ON THIS PLANET."
2250 PRINT "THIS PLANET ";:IF HM=1 THEN PRINT "MIGHT BE";:ELSE PRINT "WOULDN'T BE";
2260 PRINT " CONSIDERED HABITABLE BY MAN."
2270 INPUT "WANT ANOTHER PLANET";A$
2280 IF ASC(A$)=89 THEN 990
2290 CLS:PRINT "OTHER PLANETS"
2300 INPUT "HOW MANY PLANETS WOULD YOU LIKE";NP
2310 IF NP>15 THEN PRINT "FOR CONVENIENCE WE'LL LIMIT THIS TO 15":NP=15
2320 IF NP<1 THEN 2760
2330 AM=1179/SQR(MS)-M*SQR(RP)
2340 R(1)=RP:MP(1)=M
2350 FOR I=2 TO NP
2360 CLS
2370 PRINT "OUR SOLAR SYSTEM IS LAID OUT LIKE THIS:"
2380 PRINT "PLANET      MASS      DISTANCE FROM SUN"
2390 PRINT "MERCURY     .055         .387"
2400 PRINT "VENUS       .815         .723"
2410 PRINT "EARTH      1.0          1.0"
2420 PRINT "MARS        .108        1.524"
2430 PRINT "JUPITER  317.9          5.203"
2440 PRINT "SATURN    95.2          9.539"
2450 PRINT "URANUS    14.6         19.18"
2460 PRINT "NEPTUNE   17.2         30.06"
2470 PRINT "PLUTO       .100       39.44"
2480 PRINT "MASS FOR PLANET #";I;:INPUT MP(I)
2490 IF MP (I)>1000 THEN PRINT "A BODY THIS LARGE WOULD BECOME A STAR.":GOTO 2500
2500 INPUT "DISTANCE FROM STAR";R(I)
2510 IF R(I)<MS/5 THEN PRINT "PLANET IS TOO CLOSE TO SUN":GOTO 2500
2520 IF R(I)>56*MS THEN PRINT "PLANET IS TOO FAR FROM THE SUN":GOTO 2500
2530 FOR K=1 TO I-1:IF R(K)>.9*R(I) AND R(K)<1.1*R(I) THEN 2590
2540 NEXT K
2550 A1=MP(I)*SQR(R(I))
2560 IF A1>AM THEN PRINT "THE PLANET HAS TOO MUCH MASS FOR SYSTEM":GOTO 2370
2570 AM=AM-A1
2580 NEXT I:GOTO 2610
2590 PRINT "THIS PLANET IS TOO CLOSE TO OTHER PLANETS TO HAAVE A STABLE ORBIT"
2600 GOTO 2370
2610 FOR I=1 TO NP:F=0:FOR K=1 TO NP-I
2620 IF R(K+1)>=R(K) THEN 2660
2630 T=R(K):R(K)=R(K+1):R(K+1)=T
2640 T=MP(K):MP(K)=MP(K+1):MP(K+1)=T
2650 F=1
2660 NEXT K
2670 IF F=0 THEN 2690
2680 NEXT I
2690 PRINT "PLANET #      MASS            ORBIT"
2700 FOR I=1 TO NP
2710 PRINT I,MP(I),R(I);
2720 IF R(I)>RM AND R(I)<RX AND MP(I)>.055 AND MP(I)<17.6 THEN PRINT "LIFE?" ELSE PRINT " "
2730 NEXT I
2740 INPUT "WOULD YOU LIKE TO TRY ANOTHER SYSTEM";A$
2750 IF ASC(A$)=89 THEN 2290
2760 INPUT "WOULD YOU LIKE TO TRY ANOTHER STAR";A$
2770 IF ASC(A$)=89 THEN 270
2780 END